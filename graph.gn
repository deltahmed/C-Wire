filename = sprintf("%s", ARG)
set datafile separator ":"

# Determine the graph has negative/positive numbers or not 
stats filename using 4 nooutput
has_negative = (STATS_min < 0)
has_positive = (STATS_max > 0)

# graph y range 
y_min = (STATS_min == STATS_max ? STATS_min - abs(STATS_min) * 0.1 : STATS_min) - abs(STATS_min) * 0.2
y_max = (STATS_min == STATS_max ? STATS_max + abs(STATS_max) * 0.1 : STATS_max) + abs(STATS_max) * 0.2

set terminal pngcairo size 1000,600 enhanced font 'Arial,12'
set output "graphs/lv_all_minmax_graph.png"

set title "Graph (lv all minmax)" font ",14"
set xlabel "LV Station ID" font ",12"
set ylabel "Electrical efficiency" font ",12"
set xtics rotate by -45 font ",10"
set style data histogram
set style histogram cluster gap 1
set style fill solid 0.7 border -1
set boxwidth 0.9
set key outside top center horizontal

# set the graph y range
set yrange [y_min:y_max]

# Conditionnal plot

if (has_negative && has_positive) {
     plot filename using (column(4) < 0 ? $4 : 1/0):xtic(1) title "Negative Values" linecolor rgb "red", \
     filename using (column(4) >= 0 ? $4 : 1/0):xtic(1) title "Positive Values" linecolor rgb "green"
} else {
     if (has_negative) {
          plot filename using ($4 < 0 ? $4 : 1/0):xtic(1) title "Negative Values" linecolor rgb "red"
     }

     if (has_positive) {
          plot filename using ($4 >= 0 ? $4 : 1/0):xtic(1) title "Positive Values" linecolor rgb "green"
     }

}

